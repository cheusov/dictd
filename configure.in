dnl Process this file with autoconf to produce a configure script for dict.
dnl
dnl Created: Wed Apr 24 14:11:05 1996 by faith@cs.unc.edu
dnl Revised: Fri Jul 11 13:46:10 1997 by faith@acm.org
dnl Copyright 1996, 1997 Rickard E. Faith (faith@acm.org)
dnl
dnl This program is free software; you can redistribute it and/or modify it
dnl under the terms of the GNU General Public License as published by the
dnl Free Software Foundation; either version 1, or (at your option) any
dnl later version.
dnl
dnl This program is distributed in the hope that it will be useful, but
dnl WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License along
dnl with this program; if not, write to the Free Software Foundation, Inc.,
dnl 675 Mass Ave, Cambridge, MA 02139, USA.
dnl
dnl $Id: configure.in,v 1.12 1998/01/16 03:30:21 faith Exp $
dnl
dnl Flag conventions:
dnl     CFLAGS and LDFLAGS should be settable on the make commandline
dnl             for optimization and stripping.
dnl     WCFLAGS and WLDFLAGS are warnings and stuff that we like (e.g., -pipe)
dnl     XTRACFLAGS, XTRALDFLAGS are for things like dmalloc
dnl     LIBOBJS is an automatically-generated list of extra objects we need
dnl These aren't used for libmaa:
dnl     XCFLAGS and XLDFLAGS are for X11
dnl     YCFLAGS is for yacc
dnl     LCFLAGS is for lex

AC_PREREQ(2.1)
AC_REVISION($Revision: 1.12 $)
AC_INIT(dictd.c)
AC_CONFIG_HEADER(config.h)

prefix=/usr
etcdir=/etc
piddir=/var/run

echo Configuring for dict
echo .

AC_ARG_WITH(cc,
[  --with-cc               with cc (instead of gcc)],
CC=cc)

AC_CANONICAL_HOST
AC_PROG_CC
AC_ISC_POSIX

REALCC="$CC"
if test "$CC" = gcc; then
    CFLAGS='-g -O'
    WCFLAGS='-Wall'
else
    CFLAGS='-g'
fi

local_dmalloc=0
local_libmaa=0
local_zlib=0

case "$host" in
dnl did include: -Winline
*linux*)
    WCFLAGS="$WCFLAGS -pipe -Wwrite-strings -Wpointer-arith -Wcast-align \
        -Wmissing-prototypes -Wshadow -Wnested-externs -Waggregate-return";;
sparc-sun-sunos*)
    WCFLAGS="$WCFLAGS -pipe";;
esac

AC_ARG_WITH(cflags,
[  --with-cflags=FLAGS     use FLAGS for CFLAGS],
CFLAGS="$withval")

AC_ARG_WITH(prof,
[  --with-prof             with prof profiling],
CFLAGS="$CFLAGS -p"
LDFLAGS="$LDFLAGS -p")

AC_ARG_WITH(gprof,
[  --with-gprof            with gprof profiling],
CFLAGS="$CFLAGS -pg"
LDFLAGS="$LDFLAGS -pg")

AC_ARG_WITH(dmalloc,
[  --with-dmalloc          with Gray Watson's debugging malloc],
needs_dmalloc=1
needs_xmalloc=0)

AC_ARG_WITH(local-dmalloc,
[  --with-local-dmalloc    with dmalloc built in local source tree],
needs_dmalloc=1
local_dmalloc=1
needs_xmalloc=0)

AC_ARG_WITH(local-libmaa,
[  --with-local-libmaa     with libmaa built in local source tree],
local_libmaa=1)

AC_ARG_WITH(local-zlib,
[  --with-local-zlib       with zlib built in local source tree],
local_zlib=1)

AC_ARG_WITH(checker,
[  --with-checker          with Checker support (Linux only)],
REALCC=checkergcc)

AC_ARG_WITH(efence,
[  --with-efence           with Electric Fence (by Bruce Perens)],
REALLIBS="$REALLIBS -lefence")

AC_ARG_WITH(insure,
[  --with-insure           with Insure support (from ParaSoft Corp.)],
REALCC=insight)

AC_ARG_WITH(purify,
[  --with-purify           with Purify support (from Pure, Inc.)],
REALCC="purify gcc")

AC_ARG_WITH(etcdir,
[ --with-etcdir=PATH       directory containing server configuration file],
etcdir=$withval)

AC_ARG_WITH(piddir,
[ --with-piddir=PATH       directory containing dictd.pid file],
piddir=$withval)

dnl Checks for programs.
echo Checking for programs

AC_PROG_INSTALL
AC_PROG_LEX
AC_PROG_YACC
AC_PROG_RANLIB
AC_CHECK_PROGS(NROFF,gnroff nroff)
AC_CHECK_PROGS(TROFF,groff troff)
AC_CHECK_PROGS(COL,col cat)
AC_CHECK_PROGS(EXPAND,expand cat)

echo .
echo Checking for libraries

if test "$local_libmaa" = 0; then
    AC_CHECK_LIB(maa,maa_init,REALLIBS="$REALLIBS -lmaa",local_libmaa=1)
fi

if test "$local_libmaa" = 1; then
    AC_MSG_CHECKING(for libmaa in local source tree)
    if test -d libmaa; then
        REALLIBS="$REALLIBS -Llibmaa -lmaa"
        XTRACFLAGS="$XTRACFLAGS -Ilibmaa"
        subdirs="$subdirs libmaa"
        allsubdirs="$allsubdirs libmaa"
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
        AC_MSG_ERROR(Libmaa not found, cannot continue)
    fi
fi

if test "$local_zlib" = 0; then
    AC_CHECK_LIB(z,zlibVersion,REALLIBS="$REALLIBS -lz",local_zlib=1)
fi

if test "$local_zlib" = 1; then
    AC_MSG_CHECKING(for zlib in local source tree)
    if test -d zlib; then
        REALLIBS="$REALLIBS -Lzlib -lz"
        XTRACFLAGS="$XTRACFLAGS -Izlib"
        allsubdirs="$allsubdirs zlib"
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
        AC_MSG_ERROR(Zlib not found, cannot continue)
    fi
fi

AC_CHECK_LIB(socket,main,REALLIBS="$REALLIBS -lsocket")
AC_CHECK_LIB(nsl,main,REALLIBS="$REALLIBS -lnsl")

if test "$local_dmalloc" = 0 -a "$needs_dmalloc" = 1; then
    AC_CHECK_LIB(dmalloc,main,
        REALLIBS="$REALLIBS -ldmalloc"
        XTRACFLAGS="$XTRACFLAGS -DDMALLOC_FUNC_CHECK",
    local_dmalloc=1)
fi

if test "$local_dmalloc" = 1; then
    AC_MSG_CHECKING(for dmalloc in local source tree)
    if test -d dmalloc; then
        REALLIBS="$REALLIBS -Ldmalloc -ldmalloc"
        XTRACFLAGS="$XTRACFLAGS -DDMALLOC_FUNC_CHECK -Idmalloc"
        XTRAHEADERS="$XTRAHEADERS dmalloc/dmalloc.h"
        allsubdirs="$allsubdirs dmalloc"
        subdirs="$subdirs dmalloc"
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
    fi
fi

echo .
echo Checking for header files

AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS(unistd.h strings.h limits.h)
AC_CHECK_HEADERS(sys/resource.h sys/time.h sys/param.h sys/wait.h)

echo .
echo Checking for system constants

AC_C_CONST
AC_C_INLINE

AC_MSG_CHECKING(where to put dictd.pid)
if test '!' -d $piddir; then
    piddir=$etcdir;
fi
AC_MSG_RESULT($piddir)

echo .
echo Checking for library functions

AC_CHECK_FUNCS(strdup strtol strtoul semget strerror)
AC_REPLACE_FUNCS(strdup strtol strtoul)

echo .
echo Making output files

CC="$REALCC"
LIBS="$REALLIBS"

AC_CONFIG_SUBDIRS($subdirs)

AC_SUBST(WCFLAGS)
AC_SUBST(WLDFLAGS)
AC_SUBST(XTRACFLAGS)
AC_SUBST(XTRALDFLAGS)
AC_SUBST(XTRAHEADERS)
AC_SUBST(allsubdirs)
AC_SUBST(piddir)
AC_SUBST(etcdir)
AC_OUTPUT(Makefile doc/Makefile)

date > stamp-h.in
date > stamp-h
echo .
echo Done
